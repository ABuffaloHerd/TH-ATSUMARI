//The final background script

//Spell control
let bSpell = false;
let tileConst = 128;
let count = 0;

@Initialize
{
  SetCameraAzimuthAngle(-90);
  SetCameraElevationAngle(10);
  SetCameraFocusXYZ(0, 0, 0);
  SetCameraRadius(550);
  // SetCameraRoll(25);

  TNormalBackground;
  TSpellBackground;
}

@MainLoop
{
  let objScene = GetEnemyBossSceneObjectID();
	if(objScene != ID_INVALID && ObjEnemyBossScene_GetInfo(objScene, INFO_IS_SPELL))
	{
		bSpell = true;
	}
	else
	{
		bSpell = false;
	}

  // count++;
  yield;
}

task TNormalBackground
{
  //Floor
  let wood = GetCurrentScriptDirectory() ~ "texture/sand.png";

  let floor = ObjPrim_Create(OBJ_SPRITE_3D);
  ObjRender_SetBlendType(floor, BLEND_ALPHA);
  Obj_SetRenderPriority(floor, 0.22);
  ObjPrim_SetTexture(floor, wood);
  // ObjSprite3D_SetSourceRect(floor, 0, 0, 1024, 1024);
  // ObjSprite3D_SetDestRect(floor, -1024 * tileConst, -1024 * tileConst, 1024 * tileConst, 1024 * tileConst);
  ObjSprite3D_SetSourceDestRect(floor, 0, 0, 1024 * 8, 1024 * 8);

  // Set rotation
  ObjRender_SetAngleX(floor, 90);
  ObjRender_SetAngleY(floor, -90);

  ObjRender_SetPosition(floor, 900, -90, 90);

  let frame = 0;

  //Sky
  let sky = GetCurrentScriptDirectory() ~ "texture/clearsky.png";

  let background = ObjPrim_Create(OBJ_SPRITE_2D);
  ObjRender_SetBlendType(background, BLEND_ALPHA);
  Obj_SetRenderPriority(background, 0.21);
  ObjPrim_SetTexture(background, sky);

  ObjSprite2D_SetSourceRect(background, 0, 0, 1024, 4096);
  ObjSprite2D_SetDestRect(background, 0, 0, 1024, 1024);
  ObjRender_SetPosition(background, 0, 0, 0);

  //Mountains
  let mountain = GetCurrentScriptDirectory() ~ "texture/mount1.png";

  let mountains = ObjPrim_Create(OBJ_SPRITE_2D);
  ObjRender_SetBlendType(mountains, BLEND_ALPHA);
  Obj_SetRenderPriority(mountains, 0.211);
  ObjPrim_SetTexture(mountains, mountain);

  ObjSprite2D_SetSourceRect(mountains, 0, 0, 1024, 4096);
  ObjSprite2D_SetDestRect(mountains, 0, 0, 448, 1024);
  ObjRender_SetPosition(mountains, 0, 0, 0);

  loop
  {
    //Scroll the ground
    ObjSprite3D_SetSourceRect(floor, 0, (0 + (frame % 1024) * 0.5) * 8, 1024, (1024 + (frame % 1024) * 0.5) * 8);

    //Scroll the mountains
    ObjSprite2D_SetSourceRect(mountains, 0 + (frame * -0.3), 0, 448 + (frame * -0.3), 4096);

    frame++;
    yield;
  }

}

task TSpellBackground
{
  //Main background
  let bgSpell = GetCurrentScriptDirectory() ~ "texture/spellBG.png";

  let objBG = ObjPrim_Create(OBJ_SPRITE_2D);
  ObjRender_SetBlendType(objBG, BLEND_ALPHA);
  Obj_SetRenderPriority(objBG, 0.22);
  ObjPrim_SetTexture(objBG, bgSpell);

  ObjSprite2D_SetSourceRect(objBG, 0, 0, 384, 448);
  ObjSprite2D_SetDestRect(objBG, 0, 0, 384, 448);
  ObjRender_SetPosition(objBG, 0, 0, 1);

  //Psycho effect
  let psychoBG = GetCurrentScriptDirectory() ~ "texture/spiral-bw.png";

  let spiral = ObjPrim_Create(OBJ_SPRITE_2D);
  ObjRender_SetBlendType(spiral, BLEND_ADD_RGB);
  Obj_SetRenderPriority(spiral, 0.221);
  ObjPrim_SetTexture(spiral, psychoBG);

  ObjSprite2D_SetSourceRect(spiral, 0, 0, 1024, 1024);
  ObjSprite2D_SetDestRect(spiral, -512, -512, 512, 512);
  ObjRender_SetPosition(spiral, GetStgFrameWidth() / 2, GetStgFrameHeight() / 2, 1);

  let alpha = 0;
  let frame = 0;



  loop
  {
    if (bSpell) //if spellcard
    {
      alpha += 4;
    }
    else
    {
      alpha = 0;
    }

    Obj_SetVisible(objBG, alpha > 0);
    ObjRender_SetAlpha(objBG, alpha);

    Obj_SetVisible(spiral, alpha > 0);
    ObjRender_SetAlpha(spiral, alpha);

    ObjRender_SetAngleZ(spiral, frame * 4);

    frame++;
    yield;
  }
}