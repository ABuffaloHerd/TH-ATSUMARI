#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Nonspell1"]
#Text["AAAAAA"] 

#include "./shot/ZUNShot_Const.txt" //Some bullet graphics
#include "./lib.dnh"

//Declare some global variables
let objBoss;
let count = 0;

//Sound
let shot1 = GetCurrentScriptDirectory() ~ "./se/se_tan01.wav";
let shot2 = GetCurrentScriptDirectory() ~ "./se/se_tan00.wav";
let twinkle = GetCurrentScriptDirectory() ~ "./se/se_kira00.wav";
let tan1 = CreateSoundObject(shot1, 40);
let tan2 = CreateSoundObject(shot2, 40);
let kira = CreateSoundObject(twinkle, 40);

@Event
{
  alternative(GetEventType())
  case(EV_REQUEST_LIFE)
  {
    SetScriptResult(2000);
  }
  case(EV_REQUEST_TIMER)
  {
    SetScriptResult(99);
  }
  case(EV_REQUEST_SPELL_SCORE)
  {
    SetScriptResult(1000000);
  }
}


@Initialize 
{
  objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
  ObjEnemy_Regist(objBoss);

  //Move the boss to center of the screen: 0, 60 in 60 frames (1 second)
  ObjMove_SetDestAtFrame(objBoss, GetCenterX(), 60, 60);

  DrawLoop;
  drawMagicCircle(objBoss);
  TFinalize;
}

@MainLoop 
{
  //Boss hitbox move to boss center
  ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);

  //Player hitbox 
  ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);


  if(count > 60 && count % 90 == 0)
  {
    shootfive;
  }

  if(count > 60 && ObjEnemy_GetInfo(objBoss, INFO_LIFE) < 1000 && count % 80 == 0)
  {
    bigpew;
  }
  count++;
  yield;
}

task shootfive
{
  let angle = GetAngleToPlayer(objBoss);
  loop(15)
  {
    PlaySoundObject(tan1);
    loop(25)
    {
      CreateShotA2(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 30, angle, -2, 5, LARGE_RED, 20);
      angle += 360/25;
    }
    wait(5);
  }
}

task bigpew
{
  PlaySoundObject(tan2);
  let angle = 0;
  let obj1 = CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 20, 0, LARGE_PURPLE, 2);
  let obj2 = CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 20, 180, LARGE_PURPLE, 2);

  bigPewMovements(obj1);
  bigPewMovements(obj2);
}

task bigPewMovements(object)
{
  ObjMove_SetAcceleration(object, -1);
  while(ObjMove_GetSpeed(object) > 0)
  {
    yield;
  }

  let theta = GetAngleToPlayer(objBoss);
  PlaySoundObject(kira);
  loop(15)
  {
    CreateShotA1(ObjMove_GetX(object), ObjMove_GetY(object), 5, theta, BL_SMALL_BLUE, 10);
    theta += 360/15;
  }

  Obj_Delete(object);
}

task DrawLoop
{
  let image = GetCurrentScriptDirectory() ~ "./sprite/th16.png"; // Set okina
  ObjPrim_SetTexture(objBoss, image); // Set objBoss texture to imgRumia
  ObjSprite2D_SetSourceRect(objBoss, 10, 2304, 103, 2407); // Spritesheet coords for one texture
  ObjSprite2D_SetDestCenter(objBoss); // Set the center of the sprite
}

task TFinalize
{
  //Don't do anything until the boss life = 0
  while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0)
  {
    yield;
  }

  Obj_Delete(objBoss); //Delete boss
  DeleteShotAll(TYPE_ALL, TYPE_IMMEDIATE); // Immediately delete all shots
  SetAutoDeleteObject(true); // Auto delete objects
  CloseScript(GetOwnScriptID()); // Close the script
  return; //Fuck off
}
