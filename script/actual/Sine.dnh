#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["sine test"]
#Text["sin cos tan"] 

#include "./shot/ZUNShot_Const.txt" //Some bullet graphics
#include "./lib.dnh"

//Declare some global variables
let objBoss;
let objScene = GetEnemyBossSceneObjectID();
let count = 0;
let firstShot = false;

@Event
{
  alternative(GetEventType())
  case(EV_REQUEST_LIFE)
  {
    SetScriptResult(5000);
  }
  case(EV_REQUEST_TIMER)
  {
    SetScriptResult(99);
  }
  case(EV_REQUEST_SPELL_SCORE)
  {
    SetScriptResult(1000000);
  }
}


@Initialize 
{
  objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
  ObjEnemy_Regist(objBoss);
  ObjEnemyBossScene_StartSpell(objScene);

  //Move the boss to center of the screen: 0, 60 in 60 frames (1 second)
  ObjMove_SetDestAtFrame(objBoss, GetCenterX(), 60, 60);

  DrawLoop;
  TFinalize;
}

@MainLoop 
{
  //Boss hitbox move to boss center
  ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);

  //Player hitbox 
  ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
  if(count > 100 && not(firstShot))
  {
    bulletWall;
    firstShot = true;
  }

  if (count > 60 && count % 150 == 0)
  {
    sinShot;
  }
  count++;
  yield;
}

task sinShot
{
  let angleT;
  ascent (i in 1..360) 
  {
    angleT = sin(i) * 500;
    CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 6, angleT, KNIFE_BLUE, 1);
    CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 6, angleT * -1, KNIFE_BLUE, 1);
    wait(3);
  }
}

task bulletWall
{
  ascent (j in 0..360)
  {
    let bullets = CreateShotA1(GetPlayerX() + (100 * cos(j)), GetPlayerY() + (100 * sin(j)), 0, 0, BL_SMALL_RED, 20);
    bulletWallExpire(bullets);
  }
}

task bulletWallExpire(obj)
{
  wait(80);
  //CreateLooseLaserA1(ObjMove_GetX(obj), ObjMove_GetY(obj), 10, GetAngleToPlayer(obj) + 180, 300, 16, C_LASER_YELLOW, 1);
  wait(1);
  Obj_Delete(obj);
  wait(20);
  firstShot = false;
}

task DrawLoop
{
  let image = GetCurrentScriptDirectory() ~ "./sprite/th16.png"; // Set okina
  ObjPrim_SetTexture(objBoss, image); // Set objBoss texture to imgRumia
  ObjSprite2D_SetSourceRect(objBoss, 10, 2304, 103, 2407); // Spritesheet coords for one texture
  ObjSprite2D_SetDestCenter(objBoss); // Set the center of the sprite
}

task TFinalize
{
  //Don't do anything until the boss life = 0
  while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0)
  {
    yield;
  }

  //Add spellcard bonuses
  if(ObjEnemyBossScene_GetInfo(objScene, INFO_PLAYER_SHOOTDOWN_COUNT) + ObjEnemyBossScene_GetInfo(objScene, INFO_PLAYER_SPELL_COUNT) == 0)
  {
    AddScore(ObjEnemyBossScene_GetInfo(objScene, INFO_SPELL_SCORE));
  }

  Obj_Delete(objBoss); //Delete boss
  DeleteShotAll(TYPE_ALL, TYPE_IMMEDIATE); // Immediately delete all shots
  SetAutoDeleteObject(true); // Auto delete objects
  CloseScript(GetOwnScriptID()); // Close the script
  return; //Fuck off
}
