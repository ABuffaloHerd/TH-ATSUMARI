#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Nonspell2"]
#Text["base file for nonspells"] 

#include "./shot/ZUNShot_Const.txt" //Some bullet graphics
#include "./lib.dnh"

//Declare some global variables
let objBoss;
let count = 0;

@Event
{
  alternative(GetEventType())
  case(EV_REQUEST_LIFE)
  {
    SetScriptResult(4000);
  }
  case(EV_REQUEST_TIMER)
  {
    SetScriptResult(99);
  }
  case(EV_REQUEST_SPELL_SCORE)
  {
    SetScriptResult(1000000);
  }
}


@Initialize 
{
  objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
  ObjEnemy_Regist(objBoss);

  //Move the boss to center of the screen: 0, 60 in 60 frames (1 second)
  ObjMove_SetDestAtFrame(objBoss, GetCenterX(), 60, 60);

  DrawLoop;
  drawMagicCircle(objBoss);
  TFinalize;
}

@MainLoop 
{
  //Boss hitbox move to boss center
  ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);

  //Player hitbox 
  ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);

  if(count > 60 && count % 85 == 0)
  {
    Shoot1;
  }

  count++;
  yield;
}

task Shoot1
{
  ascent (i in 1..6)
  {
    let obj1 = CreateShotA2(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), i * 2, 20, -0.2, 0, ORB_RED, 4);
    let obj2 = CreateShotA2(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), i * 2, 160, -0.2, 0, ORB_RED, 4);
    bulletMove(obj1);
    bulletMove(obj2);
  }
}

task bulletMove(object)
{
  let bullets = [BL_SMALL_RED, BL_SMALL_ORANGE, BL_SMALL_YELLOW, BL_SMALL_GREEN];

  while(ObjMove_GetSpeed(object) > 0)
  {
    yield;
  }

  if(Obj_IsDeleted(object))
  {
    return;
  }

  ObjMove_SetAngle(object, rand(75, 105));
  ObjMove_SetAcceleration(object, 0);
  ObjMove_SetSpeed(object, 8);
  wait(rand(10, 20));

  let angleTheta = rand(0, 360);
  loop(10)
  {
    CreateShotA1(ObjMove_GetX(object), ObjMove_GetY(object), rand(1, 3), angleTheta, bullets[randInt(0, 3)], 10);
    angleTheta += 360 / 10;
  }

  Obj_Delete(object);
}

task DrawLoop
{
  let image = GetCurrentScriptDirectory() ~ "./sprite/th16.png"; // Set okina
  ObjPrim_SetTexture(objBoss, image); // Set objBoss texture to imgRumia
  ObjSprite2D_SetSourceRect(objBoss, 10, 2304, 103, 2407); // Spritesheet coords for one texture
  ObjSprite2D_SetDestCenter(objBoss); // Set the center of the sprite
}

task TFinalize
{
  //Don't do anything until the boss life = 0
  while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0)
  {
    yield;
  }

  Obj_Delete(objBoss); //Delete boss
  DeleteShotAll(TYPE_ALL, TYPE_IMMEDIATE); // Immediately delete all shots
  SetAutoDeleteObject(true); // Auto delete objects
  CloseScript(GetOwnScriptID()); // Close the script
  return; //Fuck off
}
